/* NetRexx */
options replace format comments java crossref symbols nobinary

import java.math.

runSample(arg)
return

-- ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
method bead_sort(harry = Rexx[]) public static binary returns Rexx[]
  beads = TreeMap()

  loop val over harry
    -- collect occurences of beads in TreeMap indexed on value
    kval = BigDecimal(val)
    bead = Rexx beads.get(kval)
    if beads.get(kval) == null then beads.put(kval, Rexx(1))
    else                            beads.put(kval, Rexx(bead + 1))
    end val

  harry_sorted = Rexx[harry.length]
  bi = 0
  xx = BigDecimal
  loop xx over beads.keySet()
    -- extract beads in value order and insert in result array
    loop for Rexx beads.get(xx)
      harry_sorted[bi] = Rexx(xx)
      bi = bi + 1
      end
    end xx

  return harry_sorted

-- ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
method runSample(arg) public static
  unsorted = [734, 3, 1, 24, 324, -1024, -666, -1, 0, 324, 32, 0, 999999999, 432, 42, 3, 4, 1, 1]
  sorted = bead_sort(unsorted)
  say arrayToString(unsorted)
  say arrayToString(sorted)
  return
-- ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
method arrayToString(harry = Rexx[]) private static
  list = Rexx ''
  loop vv over harry
    list = list vv
    end vv
  return '['list.space(1, ',')']'

