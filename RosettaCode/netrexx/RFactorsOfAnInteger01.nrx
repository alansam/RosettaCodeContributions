/* NetRexx */
options replace format comments java crossref symbols nobinary

/* REXX ***************************************************************
 * Program to calculate and show divisors of positive integer(s).
 * 03.08.2012 Walter Pachl
 **********************************************************************/
numL = 0
numH = 0
loop label running forever
  say 'Provide two integers [or anything else to stop]:'
  parse ask numL numH .
  if \numL.datatype('w') | \numH.datatype('w') then leave running
  Loop j_ = numL to numH
    say '   n = ' j_.right(6) "   divisors = " divs(j_)
    End j_
  end running

return
 
-- ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
method divs(x) public static
  If x == 1 then Return 1                      /*handle special case of 1     */
  LoHi = '1' x
  Parse LoHi lo hi                             /*initialize lists: lo=1 hi=x  */
  odd = x // 2                                 /* 1 if x is odd               */
  Loop j = 2 + odd By 1 + odd While j * j < x  /*divide by numbers<sqrt(x)    */
    If x // j == 0 then Do                     /*Divisible?  Add two divisors:*/
      lo = lo j                                /* list low divisors           */
      hi = x % j hi                            /* list high divisors          */
      End
    End
  If j * j = x Then                            /*for a square number as input */
    lo = lo j                                  /* add its square root         */
  Return lo hi                                 /* return both lists           */

