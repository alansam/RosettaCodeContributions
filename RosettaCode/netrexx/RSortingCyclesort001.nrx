/* REXX ***************************************************************
* 12.06.2014 Walter Pachl translated from Wikipedia's code
**********************************************************************/
Options replace format comments java crossref symbols nobinary
list='1 9 3 5 8 4 7 0 6 2'
n=list.words()
array = ''
Loop i = 0 To n - 1
  array[i] = list.word(i + 1)
  End
Say list
writes = cyclesort(array, n)
Say 'writes='writes
ol = ''
Loop i = 0 To n - 1
  ol = ol array[i]
  End
Say ol.strip
Exit

method cycleSort(array, n) public static
  writes = 0

  -- Loop through the array to find cycles to rotate.
  Loop cycleStart = 0 to n - 1 - 1
    item = array[cycleStart]

    -- Find where to put the item.
    pos = cycleStart
    Loop i = cycleStart + 1 to n - 1
      if array[i] < item Then
        pos = pos + 1
      End

    -- If the item is already there, this is not a cycle.
    if pos == cycleStart Then
      Iterate

    -- Otherwise, put the item there or right after any duplicates.
    Loop while item == array[pos]
      pos = pos + 1
      End
    Parse (array[pos] item) item array_pos
    array[pos] = array_pos
    writes = writes + 1

    -- Rotate the rest of the cycle.
    Loop while pos <> cycleStart

      -- Find where to put the item.
      pos = cycleStart
      Loop i = cycleStart + 1 to n - 1
        if array[i] < item Then
          pos = pos + 1
        End

      -- Put the item there or right after any duplicates.
      Loop while item == array[pos]
        pos = pos + 1
        End
      Parse (array[pos] item) item array_pos
      array[pos] = array_pos
      writes = writes + 1
      End
    End
  return writes

