/* NetRexx */
options replace format comments java crossref symbols nobinary
import java.util.concurrent.CountDownLatch

-- =============================================================================
class RSortingSleepsort01
  properties constant
    dflt  = '-6 3 1 4 5 2 3 -7 1 6 001 3 -9 2 5 -009 -8 4 6 1 9 8 7 6 5 -7 3 4 5 2 0 -2 -1 -5 -4 -3 -0 000 0'
  properties indirect
    startLatch = CountDownLatch
    doneLatch  = CountDownLatch
    floor      = 0
    sorted     = ''
  -- ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
  method main(args = String[]) public static
    arg = Rexx(args)
    if arg = '' then arg = dflt
    say arg
    say (RSortingSleepsort01()).sleepSort(arg)
    return
  -- ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
  method sleepSort(iArg) public
    loop mn = 1 to iArg.words()
      nn = iArg.word(mn)
      setFloor(getFloor().min(nn)) -- save smallest -ve number so we can use it as a scale
      end mn
    setStartLatch(CountDownLatch(1)) -- used to put all threads on hold until we're ready to start
    setDoneLatch(CountDownLatch(iArg.words())) -- used to indicate all work is done
    loop mn = 1 to iArg.words()
      nn = iArg.word(mn)
      Thread(SortThread(nn)).start() -- loop through input and create a thread for each element
      end mn
    getStartLatch().countDown() -- cry 'Havoc', and let slip the dogs of war.
    do
      getDoneLatch().await() -- wait for worker threads to complete
    catch ix = InterruptedException
      ix.printStackTrace()
    end
    return getSorted().strip()

-- =============================================================================
class RSortingSleepsort01.SortThread dependent implements Runnable
  properties indirect
    num
  -- ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
  method SortThread(nm)
    setNum(nm)
    return
  -- ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
  method run() public
    do
      parent.getStartLatch().await() -- wait until all threads are constructed
      wait = getNum() + parent.getFloor().abs() -- shifted by value of smallest -ve (allows -ve values to be sorted)
      wait = wait * 250 -- scale up; milliseconds are not granular enough
      Thread.sleep(wait) -- wait for this number's turn to run
      do protect parent -- lock the parent to prevent collisions
        parent.setSorted(parent.getSorted() num) -- stow the number in the results List
      end
      parent.getDoneLatch().countDown() -- this one's done; decrement the latch
    catch ie = InterruptedException
      ie.printStackTrace()
    end
    return

