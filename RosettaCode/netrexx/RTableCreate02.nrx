/* NetRexx */
options replace format comments java crossref symbols binary

import java.sql.Connection
import java.sql.Statement
import java.sql.SQLException
import java.sql.DriverManager

class RTableCreate02 public
  properties indirect constant
    addressDDL = String '' -
    ' create table Address' -
    ' (' -
    '   addrID     integer     primary key generated by default as identity,' -
    '   addrStreet varchar(50) not null,' -
    '   addrCity   varchar(50) not null,' -
    '   addrState  char(2)     not null,' -
    '   addrZip    char(10)    not null' -
    ' )'
    driver = String 'org.apache.derby.jdbc.EmbeddedDriver'
    dbName = String 'db/jdbcDemoDB'

  properties indirect
    connectionURL = String 
    conn = java.sql.Connection
    sqlStatement = java.sql.Statement

  method RTableCreate02() public
    return

  method createTable() public
    do
      Class.forName(getDriver())
      setConnectionURL('jdbc:derby:' || getDbName() || ';' || 'create=true')
      setConn(DriverManager.getConnection(getConnectionURL()))
      setSqlStatement(getConn().createStatement())
      say 'Creating table'
      getSqlStatement().execute(getAddressDDL())
      say 'Table cration complete'
      getSqlStatement().close()
      getConn().close()
      do
        -- In embedded mode, an application should shut down Derby.
        -- Shutdown throws the XJ015 exception to confirm success.
        setConnectionURL('jdbc:derby:' || ';' || 'shutdown=true')
        DriverManager.getConnection(getConnectionURL())
      catch sex = SQLException
        if sex.getSQLState().equals("XJ015") then do
          say 'Database shut down normally'
          end
        else do
          say 'Database did not shut down normally'
          signal sex
          end
      end
    catch sex = SQLException
      sex.printStackTrace()
    catch ex = ClassNotFoundException
      ex.printStackTrace()
    end
    return

  method main(args = String[]) public static
    db = RTableCreate02()
    db.createTable()
    return

