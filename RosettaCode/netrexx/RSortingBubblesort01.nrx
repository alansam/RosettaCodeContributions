/* NetRexx */

options replace format comments java crossref symbols binary

import java.util.List

placesList = [String -
    "UK  London",     "US  New York",   "US  Boston",     "US  Washington" -
  , "UK  Washington", "US  Birmingham", "UK  Birmingham", "UK  Boston"     -
]
sortedList = bubbleSort(String[] Arrays.copyOf(placesList, placesList.length))

lists = [placesList, sortedList]
loop ln = 0 to lists.length - 1
  cl = lists[ln]
  loop ct = 0 to cl.length - 1
    say cl[ct]
    end ct
    say
  end ln

return

method bubbleSort(list = String[]) public constant binary returns String[]

  rl = String[list.length]
  al = bubbleSort(Arrays.asList(list))
  al.toArray(rl)

  return rl

method bubbleSort(list = List) public constant binary returns ArrayList

  listLen = list.size
  rlist = ArrayList(list)
  loop i_ = 0 to listLen - 1
    loop j_ = i_ + 1 to listLen - 1
      cmpi = rlist.get(i_)
      cmpj = rlist.get(j_)
      if (Comparable cmpi).compareTo(Comparable cmpj) > 0 then do
        rlist.set(j_, cmpi)
        rlist.set(i_, cmpj)
        end
      end j_
    end i_

  return rlist

