/* NetRexx */

options replace format comments java crossref savelog symbols nobinary

/**
 * Loops
 * <h3>Loops/Break</h3>
 * <p>
 * Show a loop which prints random numbers (each number newly generated each loop) from 0 to 19 (inclusive).
 * If a number is 10, stop the loop after printing it, and do not generate any further numbers.
 * Otherwise, generate and print a second random number before restarting the loop.
 * If the number 10 is never generated as the first number in a loop, loop forever.
 * </p>
 * <h3>Loops/Continue</h3>
 * <p>
 * Show the following output using one loop.
 * <pre>
 * 1, 2, 3, 4, 5
 * 6, 7, 8, 9, 10
 * </pre>
 * <p>
 * Try to achieve the result by forcing the next iteration within the loop upon a specific condition,
 * if your language allows it.
 * </p>
 * <h3>Loops/Do-while</h3>
 * <p>
 * Start with a value at 0. Loop while value mod 6 is not equal to 0. Each time through the loop,
 * add 1 to the value then print it. The loop must execute at least once.
 * </p>
 * <h3>Loops/Downward for</h3>
 * <p>
 * Write a for loop which writes a countdown from 10 to 0.
 * </p>
 * <h3>Loops/For</h3>
 * <p>
 * "For" loops are used to make some block of code be iterated a number of times, setting a variable or parameter
 * to a monotonically increasing integer value for each execution of the block of code. Common extensions of this
 * allow other counting patterns or iterating over abstract structures other than the integers.
 * <p>
 * For this task, show how two loops may be nested within each other, with the number of iterations performed by
 * the inner for loop being controlled by the outer for loop. Specifically print out the following pattern by
 * using one for loop nested in another:
 * </p>
 * <pre>
 * *
 * **
 * ***
 * ****
 * *****
 * </pre>
 * <h3>Loops/For with a specified step</h3>
 * <p>
 * Demonstrate a for loop where the step value is greater than one.
 * </p>
 * <h3>Loops/Foreach</h3>
 * <p>
 * Loop through and print each element in a collection in order. Use your language's "for each" loop if it has one,
 * otherwise iterate through the collection in order with some other loop.
 * </p>
 * <h3>Loops/Infinite</h3>
 * <p>
 * Specifically print out "SPAM" followed by a newline in an infinite loop.
 * </p>
 * <h3>Loops/N plus one half</h3>
 * <p>
 * Quite often one needs loops which, in the last iteration, execute only part of the loop body.
 * The goal of this task is to demonstrate the best way to do this.
 * </p>
 * <p>
 * Write a loop which writes the comma-separated list
 * <pre>
 * 1, 2, 3, 4, 5, 6, 7, 8, 9, 10
 * </pre>
 * <p>
 * using separate output statements for the number and the comma from within the body of the loop.
 * </p>
 * <h3>Loops/Nested</h3>
 * <p>
 * Show a nested loop which searches two-dimensional array filled with random number uniformly distributed on [1..20].
 * The loops iterate rows and columns of the array printing the iterated elements of, until the value 20 is met.
 * Specifically, this task also shows how to <a href="http://rosettacode.org/wiki/Loop/Break">break</a> out of nested loops.
 * </p>
 * <h3>Loops/While</h3>
 * <p>
 * Start an integer value at 1024. Loop while it is greater than 0. Print the value (with a newline)
 * and divide it by two each time through the loop.
 * </p>
 *
 * @author Alan Sampson
 * @version 1.0
 * @see <a href="http://rosettacode.org/wiki/Loops/Break">rosettacode.org/wiki/Loops/Break</a>
 * @see <a href="http://rosettacode.org/wiki/Loops/Continue">rosettacode.org/wiki/Loops/Continue</a>
 * @see <a href="http://rosettacode.org/wiki/Loops/Do-while">rosettacode.org/wiki/Loops/Do-while</a>
 * @see <a href="http://rosettacode.org/wiki/Loops/Downward_for">rosettacode.org/wiki/Loops/Downward_for</a>
 * @see <a href="http://rosettacode.org/wiki/Loops/For_with_a_specified_step">rosettacode.org/wiki/Loops/For_with_a_specified_step</a>
 * @see <a href="http://rosettacode.org/wiki/Loops/Foreach">rosettacode.org/wiki/Loops/Foreach</a>
 * @see <a href="http://rosettacode.org/wiki/Loops/Infinite">rosettacode.org/wiki/Loops/Infinite</a>
 * @see <a href="http://rosettacode.org/wiki/Loops/N_plus_one_half">rosettacode.org/wiki/Loops/N_plus_one_half</a>
 * @see <a href="http://rosettacode.org/wiki/Loops/Nested">rosettacode.org/wiki/Loops/Nested</a>
 */

method main(args = String[]) public static
  
  loopsBreak
  loopsContinue
  loopsDoWhile
  loopsDownwardFor
  loopsFor
  loopsForSpecStep
  loopsForEach
  loopsInfinite
  loopsNplusHalf
  loopsNested
  loopsWhile

  return
  
/**
 * Loops/Break
 */
method loopsBreak public static
  say
  say 'Loops/Break'
  rn = Rexx
  rnd = Random()

  loop label lb forever
    rn = rnd.nextInt(19)
    say rn.right(3)'\-'
    if rn = 10 then leave lb
    rn = rnd.nextInt(19)
    say rn.right(3)'\-'
    end lb
  say

  return

/**
 * Loops/Continue
 */
method loopsContinue public static
  say
  say 'Loops/Continue'

  nul = '\-'
  loop i_ = 1 to 10
    say i_.right(2) || nul
    if i_ // 5 = 0 then do
      say
      iterate i_
      end
    say ', ' || nul
        
    end i_

  return

/**
 * Loops/Do-while
 */
method loopsDoWhile public static
  say
  say 'Loops/Do-while'

  i_ = 0
  loop until i_ // 6 = 0
    i_ = i_ + 1
    say i_
    end 

  return

/**
 * Loops/Downward for
 */
method loopsDownwardFor public static
  say
  say 'Loops/Downward for'

  loop i_ = 10 to 0 by -1
    say i_.right(2)
    end i_

  return

/**
 * Loops/For
 */
method loopsFor public static
  say
  say 'Loops/For'

  loop i_ = 1 to 5
    loop for i_
      say '*\-'
      end
    say
    end i_

  return

/**
 * Loops/For with a specified step
 */
method loopsForSpecStep public static
  say
  say 'Loops/For with a specified step'

  loop i_ = -1.4 to 10.6 by 1.7
    say i_.format(3, 1) || '\0'
    end i_
  say

  return

/**
 * Loops/Foreach
 */
method loopsForEach public static
  say
  say 'Loops/Foreach'

  days = ['Sunday', 'Monday', 'Tuesday', 'Wednesday', 'Thursday', 'Friday', 'Saturday']
  daysl = Arrays.asList(days)
  daysi = daysl.iterator

  loop while daysi.hasNext
    say daysi.next
    end 

  return

/**
 * Loops/Infinite
 */
method loopsInfinite public static
  say
  say 'Loops/Infinite'

  ct = 0
  loop label spam forever
    say 'SPAM'
    ct = ct + 1
    if isDebug & ct >= 8 then do
      say 'Loverly SPAM, wonderful SPAM, SPAM, SPAM, SPAM!'
      leave spam
      end
    end spam

  return

/**
 * Loops/N plus one half
 */
method loopsNplusHalf public static
  say
  say 'Loops/N plus one half'

  rs = ''
  istart = 1
  iend   = 10
  loop i_ = istart to iend
    rs = rs || i_.right(3)
    if i_ < iend then do
      rs = rs','
      end
    end i_
  say rs

  return

/**
 * Loops/Nested
 */
method loopsNested public static
  say
  say 'Loops/Nested'

  rnd = Random()
  dim2 = int[10, 10]

  loop i1 = 0 for dim2.length
    loop i2 = 0 for dim2[i1].length
      dim2[i1, i2] = rnd.nextInt(20) + 1
      end i2
    end i1

  loop x1 = 0 for dim2.length
    say Rexx(x1 + 1).right(4)': \-' 
    loop x2 = 0 for dim2[x1].length
      say Rexx(dim2[x1, x2]).right(3) || '\-'
      if dim2[x1, x2] = 20 then leave x1
      finally
        say
      end x2
    finally
      say
    end x1

  return

/**
 * Loops/While
 */
method loopsWhile public static
  say
  say 'Loops/While'

  x_ = 1024
  loop while x_ > 0
    say x_.right(6)
    x_ = x_ % 2
    end

  return

method isDebug inheritable static returns boolean
  return isTrue

method isTrue public static returns boolean
  return 1 == 1

method isFalse public static returns boolean
  return \isTrue

/*
 */

