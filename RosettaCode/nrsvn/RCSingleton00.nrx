/* NetRexx */
options replace format comments java crossref symbols binary

class RCSingleton00 public

  properties private
    _id = "@"Rexx(Integer.toHexString(this.hashCode)).right(8, 0)
    _instance = Instance

  method RCSingleton00() public
    _instance = Instance.getInstance
    return

  method main(args = String[]) public static

    HT  = Rexx '\t'
    mn1 = RCSingleton00()
    mn2 = RCSingleton00()

    say HT || mn1.squeak || HT || mn2.squeak
    mn1._instance.addRef
    say HT || mn1.squeak || HT || mn2.squeak
    mn1._instance.addRef -- Note: mn1 again
    say HT || mn1.squeak || HT || mn2.squeak
    mn1._instance.release
    say HT || mn1.squeak || HT || mn2.squeak
    mn1._instance.release -- Note: mn1 again
    say HT || mn1.squeak || HT || mn2.squeak
    return

  method squeak public returns Rexx
    rs = _id'['_instance.getId']'||':'Rexx(_instance.getRefCount).right(4, 0)
    return rs

  -- ---------------------------------------------------------------------------
  class RCSingleton00.Instance public

    properties private static
      _instance = Instance
      _refCount = int 0

  properties private
    _id = "!"Rexx(Integer.toHexString(this.hashCode)).right(8, 0)

    method Instance() private
      return

    method getInstance public static protect returns Instance
      if null = _instance then do
        _instance = Instance()
        end
      return _instance

    method getId public protect returns String
      return _id

    method addRef public protect
      _refCount = _refCount + 1
      return

    method release public protect
      if _refCount > 0 then
        _refCount = _refCount - 1
      return

    method getRefCount public protect returns int
      return _refCount


